apply from: '../versions.gradle'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion versions.android.compileSdk
    defaultConfig {
        applicationId "com.cardemory.app"

        minSdkVersion versions.android.minSdk
        targetSdkVersion versions.android.targetSdk
        versionCode versions.application.versionCode
        versionName versions.application.versionName
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    // android
    implementation "androidx.constraintlayout:constraintlayout:$versions.androidx.constraint"
    // firebase
    implementation "com.google.firebase:firebase-core:$versions.firebase.core"
    implementation "com.crashlytics.sdk.android:crashlytics:$versions.firebase.crashlytics"
    // dagger
    kapt "com.google.dagger:dagger-android-processor:$versions.frameworks.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.frameworks.dagger"
    // modules
    implementation project(":base")
    implementation project(":cardlist")
    implementation project(":cardeditor")
    implementation project(":cardsetlist")
    implementation project(":card_set_editor")
    implementation project(":train")
}

task runInstrumentationTestsOnTestLab(type: Exec) {
    commandLine './run_test_lab.sh'
}
assembleAndroidTest.dependsOn('assembleDebug')
runInstrumentationTestsOnTestLab.dependsOn('assembleAndroidTest')

apply plugin: 'com.google.gms.google-services'
